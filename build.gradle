plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'maven-publish'
}

group 'YAPI'
version '1.2-SNAPSHOT'

ext {
    uberJarName = "${group}-${version}-all.jar"
    jarName = "${group}-${version}.jar"
    libs = "${buildDir}/libs"
}

sourceCompatibility = 1.12
targetCompatibility = 1.12

mainClassName = 'main.InteractiveMain'

repositories {
    jcenter()
}

dependencies {
    compile 'ch.obermuhlner:big-math:2.3.0'
    compile 'org.fusesource.jansi:jansi:1.18'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.hamcrest:hamcrest:2.2'

//    compile 'org.jcuda:jcuda:10.1.0'
//    compile 'org.jcuda:jcublas:10.1.0'
//    compile 'org.jcuda:jcufft:10.1.0'
//    compile 'org.jcuda:jcusparse:10.1.0'
//    compile 'org.jcuda:jcusolver:10.1.0'
//    compile 'org.jcuda:jcurand:10.1.0'
//    compile 'org.jcuda:jnvgraph:10.1.0'
//    compile 'org.jcuda:jcudnn:10.1.0'
}

/*jar {

}*/

shadowJar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

    from(rootDir) {
        include 'LICENSE'
    }
    from(rootDir) {
        include 'README.md'
    }
}


publishing {
    repositories {
        maven {
            url = "${rootDir}/releases"
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'yoyosource'
            artifactId = 'YAPI'
            version = '1.2-3.2'

            from components.java
        }
    }
}



task YAPILicense {
    description 'Add the License to every file'
    group "YAPI"

    mustRunAfter classes

    doLast {
        def command = "java -classpath ${buildDir}/classes/java/main main.LicenseSystem ${buildDir}"
        def retval = command.execute().text
        print(retval)
    }
}
shadowJar.dependsOn YAPILicense

task YAPIInfo {
    description 'Create the Interactive Main Info file'
    group "YAPI"

    mustRunAfter YAPILicense

    doLast {
        def command = "java -classpath ${buildDir}/classes/java/main main.CreateInteractiveMain ${buildDir}"
        def retval = command.execute().text
        print(retval)
    }
}
shadowJar.dependsOn YAPIInfo

task buildYAPI {
    description 'Build YAPI'
    group "YAPI"

    dependsOn 'build'

    doLast {
        if ("${buildDir}" == null) {
            return
        }
        delete fileTree("${libs}").matching {
            exclude("${uberJarName}")
        }
        file(libs + "/" + uberJarName).renameTo(file(libs + "/" + jarName))
    }
}
